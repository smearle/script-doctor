title Sokodig
author Ezra Szanton
homepage ezras.work

========
OBJECTS
========

stoneBackground
BLACK darkgray
11111
11111
11111
11111
11111

grassBackground
LIGHTGREEN GREEN
11111
01111
11101
11111
10111

Target
darkred red lightred
.....
.202.
.010.
.202.
.....

Wall
BROWN DARKBROWN
11101
10111
11110
00111
11001

Player0Dig
Black Orange White Blue
.000.
02020
00000
.000.
.0.0.

Player1Dig
#573103 grey #6e6c69 
..0..
..0..
..0..
.111.
..1..

Player2Dig
#573103 grey #6e6c69 
..0..
..0..
.121.
.111.
..1..

Crate
Orange darkred red lightred
22122
2...2
1...1
2...2
22122

Door
darkbrown
.....
.....
..0..
.....
.....

temp
brown
.....
.....
.....
.....
.....

stairs
 #565756 #373b37 #212421 #1c1c29 Black
00000
11111
22222
33333
44444

shovel
grey #573103
....1
...1.
0.1..
00...
000..

goldShovel
yellow #573103
....1
...1.
0.1..
00...
000..

SuperPlayer
#573103 yellow #d4bc37
..0..
..0..
.121.
.111.
..1..


=======
LEGEND
=======

Player = Player0Dig or Player1Dig or Player2Dig or SuperPlayer
Background = stoneBackground or grassBackground


# = Wall
. = stoneBackground
P = Player0Dig
* = Crate
@ = Crate and Target
O = Target
d = Door
e = Player0Dig
g = goldShovel
s = stairs
h = shovel
r = grassBackground


=======
SOUNDS
=======

Crate MOVE 36772507
Player move 17705907

sfx0 66043100 (clink)
sfx1 25555704 (dig)
sfx2 51411503 (super)

================
COLLISIONLAYERS
================

Background
Target, Door, Stairs
Player, Wall, Crate, goldShovel, shovel
temp

======
RULES
======

([DOWN Door] no [ Target no Crate] -> [] )

( no [Target no Crate] -> [] no [no Crate] )

[ >  Player | shovel ] -> [  | Player2Dig  ]

[ >  Player | goldShovel ] -> [  | SuperPlayer  ] sfx2

[ >  Player | Crate ] [stairs] -> [   Player | Crate  ] [stairs] sfx0

[ >  Player | Crate ] -> [  >  Player | > Crate  ] 

[ >  Player2Dig | Wall ] -> [  | Player1Dig] sfx1

[ >  Player1Dig | Wall ] -> [  | Player0Dig] sfx1

[ >  Player0Dig | Wall ] -> [  Player0Dig | Wall ] sfx0

[ >  SuperPlayer | Wall ] -> [  | SuperPlayer] sfx1

(Altogether makes the rule that the stairs will appear if all crates are on targets)
late [Target no Crate] [] -> [Target no Crate] [temp] 
late random [door no temp] -> [ stairs ] 
late [temp] -> [] 

==============
WINCONDITIONS
==============

all player on stairs

=======
LEVELS
=======

message 1/10 runes in the stone

#########
#########
##.....##
##pd.*o##
##.....##
#########
#########  

message 2/10 just me and my shovel

#############
#############
##dph##.*o.##
#############
#############

message 3/10 Remember, z to undo, r to restart

############
############
#######d.###
##ph###.*o##
############
###.....####
############
############

message 4/10 How long have I been down here

#######
#######
##...##
##*o.##
##hd.##
##.p.##
#######
#######

message 5/10 I can only go, down

#######
#.....#
#.oo..#
####*.#
####*.#
####dp#
####h.#
#######

message 6/10 I lost my shovel ;-;

#######
#.o####
##*.###
##*h###
#.p*..#
#o.d..#
#....o#
#######

message 7/10 riddles in the earth

###########
#.........#
#.........#
#.........#
#..####...#
#...*.#.o.#
#..#**#.o.#
#..##.#.o.#
#.........#
#.Ph....d.#
#.........#
###########

message 8/10 will I ever see the light again

##########
##########
##o.hp.d##
##.##*#.##
##.#@@#.##
##......##
##########
##########

message 9/10 I sense a great pressure

###########
###p..#h###
###.#######
#...###...#
#.*.#d#**.#
###h###ooo#
###########

#########
####p####
####.####
####.####
####.####
####.####
####.####
####s####
#########

#######
###p###
###.###
###.###
###.###
###s###
#######

#####
##p##
##.##
##s##
#####

message 10/10 my final trial

#######
#######
##oo.##
###**##
###dp##
####h##
#######
#######


message 11/10 deepest~

#############
#p.h##g##...#
#########.d.#
#########...#
#############
#############
#############
#############
#############
#.*.#########
#...#########
#.o.#########
#############

###########
#####s#####
###########
###########
###########
###########
###########
###########
#####g#####
#####p#####
###########

#########
####s####
#########
#########
#########
#########
####g####
####p####
#########

#######
###s###
#######
#######
###g###
###p###
#######

message 0/10 freedom

rrrr#####rrrr
rrrrr#s#rrrrr
rrrrrrrrrrrrr
rrrrrrrrrrrrr
rrrrrrrrrrrrr
rrrrrrrrrrrrr
rrrrrrrrrrrrr
rrrrrrrrrrrrr
rrrrrrrrrrrrr
rrrrrrrrrrrrr
rrrrr###rrrrr
rrrr#####rrrr
rrr###h###rrr
#####rpr#####






